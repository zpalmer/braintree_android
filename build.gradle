// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.3'

        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'

        classpath 'org.rauschig:jarchivelib:0.6.0'
        classpath 'commons-io:commons-io:2.4'
        classpath files('libs/0.12.1-SNAPSHOT/gradle-plugin-0.12.1-SNAPSHOT.jar')
    }
}

allprojects {
    repositories {
        flatDir {
            dirs "${rootDir}/libs"
        }
        jcenter()
        maven {
            url 'https://oss.sonatype.org/content/repositories/releases/'
        }
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }

    apply plugin: 'android-sdk-manager'

    apply plugin: 'maven'
    def sonatypeUsername = System.properties.containsKey('sonatypeUsername') ? System.properties['sonatypeUsername'] : ''
    def sonatypePassword = System.properties.containsKey('sonatypePassword') ? System.properties['sonatypePassword'] : ''
    uploadArchives {
        repositories {
            mavenDeployer {
                //beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
                snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    url 'https://github.com/braintree/braintree_android'

                    scm {
                        url 'scm:git@github.com:braintree/braintree_android.git'
                        connection 'scm:git@github.com:braintree/braintree_android.git'
                        developerConnection 'scm:git@github.com:braintree/braintree_android.git'
                    }

                    developers {
                        developer {
                            id 'devs'
                            name 'Braintree Payments'
                        }
                    }

                    licenses {
                        license {
                            name 'MIT'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }
                }
            }
        }
    }


    //apply plugin: 'signing'
    //signing {
    //    sign configurations.archives
    //}

    version = '2.5.3-SNAPSHOT'
    ext {
        compileSdkVersion = 25
        buildToolsVersion = '25.0.2'
        minSdkVersion = 16
        targetSdkVersion = 25
        versionCode = 66
        versionName = version
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
